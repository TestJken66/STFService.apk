apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.18'
    }
}

android {
    compileSdkVersion 31

    signingConfigs {
        debug {
            storeFile file('keys/debug.keystore')
            storePassword 'insecure-store-password'
            keyAlias 'STFDebugKey'
            keyPassword 'insecure-key-password'
        }
    }

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 10
        versionName "2.4.8"
        applicationId "jp.co.cyberagent.stf"
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            signingConfig signingConfigs.debug
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
  compileOptions {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
  }
}

dependencies {
    //TODO consider migrating to androidx
    //noinspection GradleCompatible
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.google.protobuf:protobuf-lite:3.0.1'
    compileOnly fileTree(dir: '../shim/libs', include: ['*.jar'])
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.20.1'
    }
    plugins {
        javalite {
            artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                javalite { }
            }
        }
    }
}
